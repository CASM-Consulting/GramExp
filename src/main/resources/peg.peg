# Hierarchical syntax
Grammar <- Spacing Mode? Definition+ EndOfFile
Mode <- '/' (!'/' Char)+ '/' Spacing
Definition <- Identifier LEFTARROW Expression
Expression <- Sequence (SLASH Sequence)*
Sequence <- Prefix*
Prefix <- (AND / NOT)? Suffix / AOPEN Suffix Literal ACLOSE
Suffix <- Primary (QUESTION / STAR / PLUS)? (PUSH / POP)?
Primary <- Identifier !LEFTARROW
/ OPEN Expression CLOSE
/ Literal / Class / DOT / EMPTY / NOTHING
# Lexical syntax
Identifier <- IdentStart IdentCont* Arguments? Spacing
IdentStart <- [a-zA-Z_]
IdentCont <- IdentStart / [0-9]
Arguments <- AOPEN (Identifier / Literal)+ ACLOSE
Literal <- ['] (!['] Char)* ['] Spacing
/ ["] (!["] Char)* ["] Spacing
Class <- '[' (!']' Range)* ']' Spacing
Range <- Char '-' !']' Char / Char
Char <- '\\' [nrt'"\[\]\\]
/ '\\' [0-2][0-7][0-7]
/ '\\' [0-7][0-7]?
/ !'\\' .
LEFTARROW <- '<-' Spacing
SLASH <- '/' Spacing
AND <- '&' Spacing
NOT <- '!' Spacing
QUESTION <- '?' Spacing
STAR <- '*' Spacing
PLUS <- '+' Spacing
OPEN <- '(' Spacing
CLOSE <- ')' Spacing
DOT <- '.' Spacing
EMPTY <- ':' Spacing
NOTHING <- '~' Spacing
COPEN <- '{' Spacing
CCLOSE <- '}' Spacing
AOPEN <- '<' Spacing
ACLOSE <- '>' Spacing
PUSH <- 'push' Spacing
POP <- 'pop' Spacing
Spacing <- (Space / Comment)*
Comment <- '#' (!EndOfLine .)* EndOfLine
Space <- ' ' / '\t' / EndOfLine
EndOfLine <- '\r\n' / '\n' / '\r'
EndOfFile <- !.